apply plugin: 'jacoco'
jacoco {
    toolVersion = "0.7.9"
}

project.apply{
    project.extensions.create("coverage", CoverageExtension)
}

project.afterEvaluate {
    if (project.coverage.includes == null) {
        project.coverage.includes = []
    }

    // Grab all build types and product flavors
    def buildTypes = android.buildTypes.collect { type ->
        type.name
    }
    def productFlavors = android.productFlavors.collect { flavor ->
        flavor.name
    }

    // When no product flavors defined, use empty
    if (!productFlavors) productFlavors.add('')

    def coverageTasks = []

    productFlavors.each { productFlavorName ->
        buildTypes.each { buildTypeName ->
            def sourceName, sourcePath
            if (!productFlavorName) {
                sourceName = sourcePath = "${buildTypeName}"
            } else {
                sourceName = "${productFlavorName}${buildTypeName.capitalize()}"
                sourcePath = "${productFlavorName}/${buildTypeName}"
            }
            def testTaskName = "test${sourceName.capitalize()}UnitTest"
            // Create coverage task of form 'testFlavorTypeCoverage' depending on 'testFlavorTypeUnitTest'

            coverageTasks = coverageTasks + "${testTaskName}Coverage"

            task "${testTaskName}Coverage"(type: JacocoReport, dependsOn: "$testTaskName") {
                group = "Reporting"
                description = "Generate Jacoco coverage reports on the ${sourceName.capitalize()} build."
                classDirectories = fileTree(
                        dir: "${project.buildDir}/intermediates/classes/${sourcePath}",

                        includes: project.coverage.includes,
                        excludes: [
                                '**/*_MembersInjector**',
                                '**/*_ViewBinding**',
                                '**/*_Factory**',
                                '**/*PermissionsDispatcher**',
                                '**_Provide*',
                                'com/babylon/v2/dagger**',
                                '**/*$*$Parcelable**',
                                '**/R.class',
                                '**/R$*.class',
                                '**/BuildConfig.*',
                                '**/Manifest*.*',
                                '**/*Test*.*',
                                'android/**/*.*']
                )
                def coverageSourceDirs = [
                        "src/main/java",
                        "src/$productFlavorName/java",
                        "src/$buildTypeName/java"
                ]
                additionalSourceDirs = files(coverageSourceDirs)
                sourceDirectories = files(coverageSourceDirs)
                executionData = files("${project.buildDir}/jacoco/${testTaskName}.exec")
                reports {
                    xml.enabled = true
                    html.enabled = true
                }
            }
        }
    }
    task testCoverage {
        coverageTasks.each {coverageTask ->
            dependsOn coverageTask
        }
    }
}

class CoverageExtension {
    List<String> includes
}